// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def OUT = Trace.OUT;
def X = WspOneRegistration.register(X86_64Linux_WspOneModule.new());
// Linux system call numbers
def SYS_openat = 257;
def SYS_clock_get_time = 228;
def SYS_clock_get_res = 229;
def SYS_getrandom = 318;
def SYS_mkdir = 83;
def SYS_link = 86;

// mkdir mode is 0o777
def MKDIR_MODE = 0x1FF;
// openat flags
def O_CLOEXEC = 0x80000;
def O_PATH = 0x200000;
def O_DIRECTORY = 0x10000;

// IO buffers
def timespec = Array<long>.new(2);

// A special value, which tells the fd of the current working directory used in openat
def AT_FDCWD = -100;

// Platform-specific implementation of "wasi_snapshot_preview1" module.
class X86_64Linux_WspOneModule extends WspOneModule {
	def init_p(err: ErrorGen) {
		def dirs = WasiOptions.getDirs();
		// execute openat syscall for every given directory
		for (ii < dirs.length) {
			var dir = dirs[ii];
			// call openat system call
			var fd = Linux.syscall(SYS_openat, 
				(AT_FDCWD,
				 Pointer.atContents(dir),
				 LinuxConst.O_RDONLY | O_CLOEXEC | O_PATH | O_DIRECTORY
				)).0;
			if (fd < 0) {
				err.abs(0).InvalidPreopenPath(dir);
			}
			var wasi_fd = fdmap.alloc();
			fdmap.set(wasi_fd, WasiFd.new(int.view(fd), wasi_filetype.DIRECTORY, true, dir));
		}
	}

	def random_get(args: Range<Value>) -> HostResult {
		var ptr = Values.v_u(args[0]), len = Values.v_u(args[1]);
		var range = memory.range_ol_32(ptr, len);
		if (range.reason != TrapReason.NONE) return HostResult.Throw(Trap.new(range.reason, null, null));
		var t = Linux.syscall(SYS_getrandom, (Pointer.atContents(range.result), len, 0));
		if (t.0 < 0) return i(mapErrno(t.0));
		return HostResult.Value1(Values.I32_0);
	}
	def clock_res_get(args: Range<Value>) -> HostResult {
		var clock_id = mapClockId(Values.v_u(args[0]));
		if (clock_id < 0) return HostResult.Value1(Values.i_v(wasi_errno.INVAL.code));
		var ptr = Values.v_u(args[1]);
		var range = memory.range_ol_32(ptr, 8);
		if (range.reason != TrapReason.NONE) return HostResult.Throw(Trap.new(range.reason, null, null));
		var t = Linux.syscall(SYS_clock_get_res, (clock_id, Pointer.atContents(timespec)));
		if (t.0 < 0) return i(mapErrno(t.0));
		DataWriters.write_range_i64(range.result, timespec[0] * 1000000000L + timespec[1]);
		return HostResult.Value1(Values.I32_0);
	}
	def clock_time_get(args: Range<Value>) -> HostResult {
		var clock_id = mapClockId(Values.v_u(args[0]));
		if (clock_id < 0) return HostResult.Value1(Values.i_v(wasi_errno.INVAL.code));
		var lag = Values.v_l(args[1]);
		var ptr = Values.v_u(args[2]);
		var range = memory.range_ol_32(ptr, 8);
		if (range.reason != TrapReason.NONE) return HostResult.Throw(Trap.new(range.reason, null, null));
		var t = Linux.syscall(SYS_clock_get_time, (clock_id, Pointer.atContents(timespec)));
		if (t.0 < 0) return i(mapErrno(t.0));
		DataWriters.write_range_i64(range.result, timespec[0] * 1000000000L + timespec[1]);
		return HostResult.Value1(Values.I32_0);
	}
	def mapClockId(clock_id: u32) -> int {
		if (clock_id > 3) return -1;
		return int.view(clock_id); // WASI clock ids map directly onto Linux clock IDs
	}
	def mapErrno(errno: i64) -> int {
		return int.!(errno);
	}

	def path_create_directory(args : Range<Value>)->HostResult {
    		var fd = Values.v_i(args[0]), ptr = args[1];
		var wfd = fdmap.get(fd);
		if (wfd == null) return i(wasi_errno.BADF.code);
		var ptr_len = args[2];
		var buf = copyString(memory, ptr, ptr_len); 

		if (!buf.ok()) return i(wasi_errno.INVAL.code);
		var path_arr = buf.result;

                var t = WasiPathUtil.resolve_path(wfd, path_arr);
                if (t.1 != wasi_errno.SUCCESS) return i(t.1.code);

                var syscall_result = Linux.syscall(SYS_mkdir, (Pointer.atContents(path_arr), MKDIR_MODE));
                if (syscall_result.0 < 0) return i(mapErrno(syscall_result.0));
                return i(wasi_errno.SUCCESS.code);

	}

	def path_link(args : Range<Value>)->HostResult { 
		var old_fd = Values.v_i(args[0]), old_flags = Values.v_u(args[1]), 
			old_path = args[2], new_fd = Values.v_i(args[3]), new_path = args[4];
		var old_wfd = fdmap.get(old_fd), new_wfd = fdmap.get(new_fd);
		if (old_wfd == null || new_wfd == null) return i(wasi_errno.BADF.code);
		var old_path_arr = copyString(memory, old_path, args[5]);
		if (!old_path_arr.ok()) return i(wasi_errno.INVAL.code);
		var new_path_arr = copyString(memory, new_path, args[6]);
		if (!new_path_arr.ok()) return i(wasi_errno.INVAL.code);
		var t = WasiPathUtil.resolve_path(old_wfd, old_path_arr.result);
		if (t.1 != wasi_errno.SUCCESS) return i(t.1.code);
		var old_resolved = t.0;
		t = WasiPathUtil.resolve_path(new_wfd, new_path_arr.result);
		if (t.1 != wasi_errno.SUCCESS) return i(t.1.code);
		var new_resolved = t.0;
		var syscall_result = Linux.syscall(SYS_linkat,
			(old_fd, Pointer.atContents(old_resolved), 
			new_fd, Pointer.atContents(new_resolved), old_flags));
		if (syscall_result.0 < 0) return i(mapErrno(syscall_result.0));
		return i(wasi_errno.SUCCESS.code);
	 }
	// def path_open(args : Range<Value>)->HostResult { return UNIMPLEMENTED; }
	// def path_readlink(args : Range<Value>)->HostResult { return UNIMPLEMENTED; }
	// def path_remove_directory(args : Range<Value>)->HostResult { return UNIMPLEMENTED; }



}

// Copyright 2023 Wizard Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def E = MonitorRegistry.register(
	"hotness", "Tallies \"hot\" execution path counts proportionally.",
	HotnessMonitor.new());
def inCodeMode = E // TODO: return Option<int>
	.addOption("code|c", "emit report with code");

// Implements a monitor that computes various measures of hotness of the program.
class HotnessMonitor extends Monitor {
	var hotness: HotnessData;
	var inCodeMode = false;

	def configure(args: string) -> string {
		def argMap = MonitorUtil.argMapFrom(args);
		if (argMap.has("code") || argMap.has("c")) {
			inCodeMode = true;
			return null;
		}
		def buf = StringBuilder.new();
		buf.put1("invalid argument(s) \"%s\"", args);
		return buf.toString();
	}

	def onParse(m: Module) {
		hotness = HotnessData.new(m);
		var it = BytecodeIterator.new();

		for (i < m.functions.length) {
			var f = m.functions[i];
			if (f.imp != null) continue; // skip imported functions

			for (bi = it.reset(f); bi.more(); bi.next()) {
				def p = hotness.newProbe(f, bi.current(), bi.pc);
				Instrumentation.insertLocalProbe(m, f.func_index, bi.pc, p);
			}
		}
	}
	def onExit(code: int) {
		onFinish(null);
	}
	def onFinish(i: Instance) {
		if (inCodeMode) hotness.reportCode();
		else hotness.report();
	}
}

def OUT = Trace.OUT;
private class HotnessData(m: Module) {
	def counters = Vector<LocCounter>.new();
	var thresholds = [0.5d, 0.75d, 0.9d, 0.95d, 0.99d, 1d];
	def spectrum = Palette.spectrum;

	def newProbe(f: FuncDecl, op: Opcode, pc: int) -> LocCounter {
		var p = LocCounter.new(f, pc, op, 0);
		counters.put(p);
		return p;
	}

	def initialize(sort: bool) -> Array<LocCounter> {
		var result = if(sort, counters.extract(), counters.copy());
		result = Arrays.sort(result, 0, result.length, LocCounter.compare);
		var total = 0uL;
		for (r in result) total += r.count;
		var sum = 0uL;
		var t = 0;
		var threshold = u64.truncd(thresholds[t] * double.roundi(total));

		for (i < result.length) {
			var r = result[i];
			r.group = t; // set hotness group
			sum += r.count;
			if (sum >= threshold) {
				t++;
				if (t >= thresholds.length) threshold = u64.max;
				else threshold = u64.truncd(thresholds[t] * double.roundi(total));
			}
		}

		return result;
	}

	def report() {
		var result = initialize(true);

		for (i < result.length) {
			var r = result[i];

			var width = 25;
			if (i > 0 && r.group > result[i-1].group) {
				var t = result[i-1].group;
				OUT.puts(spectrum[t]).puts(Palette.SEPARATOR);
				var s1 = " %d instrs", s2 = "reach %d%";
				OUT.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, s1, i)), 10)
					.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, s2, u64.truncd(100*thresholds[t]))), width)
					.puts(Palette.DEFAULT)
					.outln();
			}

			var funcColor = if(r.count > 0, Palette.FUNC, Palette.UNREACHABLE);
			var lineColor = if(r.count > 0, Palette.LNO, Palette.UNREACHABLE);

			OUT.puts(funcColor)
				.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, "func %q", r.func.render(m.names, _))), 15)
				.puts(Palette.DEFAULT)
				.puts(lineColor)
				.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, "+%d", r.pc)), 5)
				.puts(Palette.DEFAULT)
				.putc(':')
				.puts(spectrum[r.group])
				.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.putd(_, r.count)), 14)
				.puts(Palette.DEFAULT)
				.outln();
		}

	}

	def reportCode() {
		initialize(false);

		for (i < thresholds.length) {
			var width = 15;
			OUT.puts(spectrum[i]).puts(Palette.SEPARATOR);
			var s = " reach %d%";
			OUT.rjustify('-', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, s, u64.truncd(100*thresholds[i]))), width)
				.puts(Palette.DEFAULT)
				.outln();
		}

		var prev: FuncDecl;
		var bi = BytecodeIterator.new();
		var it = InstrTracer.new();
		var controlStack = ArrayStack<Opcode>.new();

		for (i < counters.length) {
			var e = counters[i];
			if (e.func != prev) {
				bi.reset(e.func);
				prev = e.func;
				OUT.puts(Palette.FUNC)
					.put1("func %q:", prev.render(m.names, _))
					.outln();
			}
			bi.skipToPc(e.pc);
			OUT.puts(Palette.DEFAULT)
				.puts(Palette.LNO)
				.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, "+%d", e.pc)), 5)
				.puts(spectrum[e.group])
				.rjustify(' ', TraceUtil.renderedLength(OUT, TraceBuilder.put1(_, "%d\t", e.count)), 8);
			match(e.op) {
				BLOCK, LOOP, IF =>  {
					for(i < controlStack.top) OUT.putc('\t');
					bi.traceOrig(OUT, m, it);
					OUT.outln();
					controlStack.push(e.op);
					continue;
				}
				END => controlStack.pop();
				_ => ;
			}
			if (e.op == Opcode.ELSE) for(i < controlStack.top - 1) OUT.putc('\t');
			else for(i < controlStack.top) OUT.putc('\t');
			bi.traceOrig(OUT, m, it);
			OUT.outln();
		}
	}
}

private class LocCounter(func: FuncDecl, pc: int, op: Opcode, var group: int) extends CountProbe {
	def compare(that: LocCounter) -> bool {
		if (this.count > that.count) return true;
		if (this.count < that.count) return false;
		if (this.func.func_index < that.func.func_index) return true;
		if (this.func.func_index > that.func.func_index) return false;
		if (this.pc < that.pc) return true;
		return false;
	}
}
